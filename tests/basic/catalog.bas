100 CALL CLEAR
110 DIM TYPE$(6)
120 TYPE$(1) = "DIS/FIX"
130 TYPE$(2) = "DIS/VAR"
140 TYPE$(3) = "INT/FIX"
150 TYPE$(4) = "INT/VAR"
160 TYPE$(5) = "PROGRAM"
165 TYPE$(6) = "DIR"
170 INPUT "DEVICE? ":DEV$
210 OPEN #1:DEV$&".",INPUT,RELATIVE,INTERNAL
220 INPUT #1:A$,J,J,K
230 DISPLAY DEV$;" - DISKNAME=";A$;"AVAILABLE=";K;"USED=";J-K
240 DISPLAY :"FILENAME   SIZE TYPE       P":"---------- ---- ---------- -";
250 FOR LOOP=1 TO 127
260 INPUT #1:A$,A,J,K
270 IF LEN(A$)=0 THEN 350
280 DISPLAY :A$;TAB(12);J;TAB(17);TYPE$(ABS(A));
290 IF ABS(A) = 5 THEN 320
300 B$=" "&STR$(K)
310 DISPLAY SEG$(B$,LEN(B$)-2,3);
320 IF A>0 THEN 340
330 DISPLAY TAB(28);"Y";
340 NEXT LOOP
350 CLOSE #1
